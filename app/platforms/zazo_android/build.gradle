buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.zazoapp.client"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 120
        versionName "2.8.9" //<!-- {MAIN_VERSION}.{FEATURE_SET}.{REVISION}-->
        resConfigs "en"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DISPATCH_ENVIRONMENT", "\"production\""
        }

        debug {
            debuggable true
            buildConfigField "String", "DISPATCH_ENVIRONMENT", "\"development\""
        }
    }
    applicationVariants.all {
        variant ->
            appendVersionNameVersionCode(variant, defaultConfig)
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.android.gms:play-services-gcm:7.5.0'
    compile 'cz.msebera.android:httpclient:4.3.3'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-codec:commons-codec:1.5'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.2.1'
    compile 'com.amazonaws:aws-android-sdk-core:2.2.5'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.amazonaws:aws-android-sdk-s3:2.2.5'
    compile 'com.balysv.materialmenu:material-menu-toolbar:1.5.4'
    compile 'com.android.support:design:23.1.0'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}

def appendVersionNameVersionCode(variant, defaultConfig) {
    //check if staging variant
    if (variant.name == android.buildTypes.release.name) {
        variant.outputs.each { output ->
            def alignedOutputFile = output.outputFile
            def unalignedOutputFile = output.packageApplication.outputFile

            // Customise APK filenames (to include build version)
            if (variant.buildType.zipAlignEnabled) {
                // normal APK
                output.outputFile = new File(alignedOutputFile.parent, "zazo-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk")
            }
            // 'unaligned' APK
            output.packageApplication.outputFile = new File(unalignedOutputFile.parent, "zazo" + defaultConfig.versionName + "-u" + ".apk")
        }
    }
}
